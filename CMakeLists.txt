cmake_minimum_required(VERSION 3.10)
project(SLISC)

# == options ===
set(opt_compiler "g++" CACHE STRING "")
set(opt_octave ON CACHE BOOL "use Octave for code generation")
set(opt_long32 ON CACHE BOOL "use 32bit integer or 64bit integer for index type (Long)")
set(opt_debug ON CACHE BOOL "debug mode")
set(opt_min ON CACHE BOOL "minimum build, disable all 3rd party libraries")
set(opt_asan ON CACHE BOOL "address sanitizer")
set(opt_std "c++11" CACHE STRING "c++ standard")
set(opt_static OFF CACHE BOOL "static compilation")
set(opt_main ON CACHE BOOL "minimum build, disable all 3rd party libraries")
set(opt_quadmath OFF CACHE BOOL "enable quad precision support")
set(opt_lapack "reference" CACHE STRING "LAPACK: [reference|mkl|none]")
set(opt_mplapack ${opt_quadmath} CACHE BOOL "enable MPLAPACK")
set(opt_boost ON CACHE BOOL "enable Boost")
set(opt_gsl ON CACHE BOOL "enable GSL")
set(opt_eigen ON CACHE BOOL "enable Eigen")
set(opt_arb ON CACHE BOOL "enable Arb")
set(opt_arpack ON CACHE BOOL "enable Arpack")
set(opt_sqlite OFF CACHE BOOL "enable Sqlite")
set(opt_sqlitecpp ON CACHE BOOL "enable Sqlitecpp")
set(opt_matfile OFF CACHE BOOL "enable Matlab .mat file (deprecated)")
# ==============

message(STATUS "------------------------")

# --- version ---
set(ver_major 0)
set(ver_minor 2)
set(ver_patch 8)

# --- init convig.h ---
set(CONFIG_H "// this file is auto generated by Makefile/CMake\n")
set(CONFIG_H "${CONFIG_H}#define SLS_MAJOR ${ver_major}\n")
set(CONFIG_H "${CONFIG_H}#define SLS_MINOR ${ver_minor}\n")
set(CONFIG_H "${CONFIG_H}#define SLS_PATCH ${ver_patch}\n")

# === minimum build ===
if(opt_min)
    set(opt_quadmath OFF)
	set(opt_mplapack OFF)
	set(opt_lapack "none")
	set(opt_boost OFF)
	set(opt_gsl OFF)
	set(opt_eigen OFF)
	set(opt_arb OFF)
	set(opt_arpack OFF)
	set(opt_sqlite OFF)
	set(opt_sqlitecpp OFF)
	set(opt_matfile OFF)
	set(opt_asan OFF)
endif()

# === compiler ====
find_program(tmp ${opt_compiler})
set(CMAKE_CXX_COMPILER ${tmp})
message(STATUS "compiler: ${CMAKE_CXX_COMPILER}")

set(CMAKE_CXX_FLAGS "-std=${opt_std}")

# === Debug/Release ===
if(opt_debug)
	message(STATUS "Build: debug")
	set(CONFIG_H "${CONFIG_H}#define SLS_CHECK_BOUNDS\n")
	set(CONFIG_H "${CONFIG_H}#define SLS_CHECK_SHAPES\n")
	add_compile_options(-g -ftrapv)
else()
	message(STATUS "Build: release")
	set(CONFIG_H "${CONFIG_H}#define NDEBUG\n")
	add_compile_options(-O3)
endif()

# === main.x ===
if(opt_main)
	message(STATUS "main.x: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_TEST_ALL\n")
else()
	message(STATUS "main.x: off")
	message(FATAL_ERROR "not implemented!")
endif()

# === Address Sanitizer ===
if(opt_asan)
	message(STATUS "asan: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_ASAN\n")
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fno-omit-frame-pointer -fsanitize=address")
	set(CMAKE_LINKER_FLAGS "${CMAKE_LINKER_FLAGS} -fno-omit-frame-pointer -fsanitize=address -static-libasan")
else()
	message(STATUS "asan: off")
endif()

# === 64bit index ===
if(opt_long32)
	message(STATUS "Long bits: 32")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_INT_AS_LONG\n")
else()
	message(STATUS "Long bits: 64")
endif()

if(opt_lapack STREQUAL "reference")
	# === CBLAS ===
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_CBLAS\n")
	if(NOT opt_long32)
		message(STATUS "CBLAS: ref64")
		set(cblas_lib cblas64 blas64 gfortran)
	else()
		message(STATUS "CBLAS: ref32")
		set(cblas_lib cblas blas gfortran)
	endif()

	# === LAPACKE ===
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_LAPACKE\n")
	if(NOT opt_long32)
		message(STATUS "LAPACKE: ref64")
		set(lapacke_lib lapacke64 lapack64)
	else()
		message(STATUS "LAPACKE: ref32")
		set(lapacke_lib lapacke lapack)
	endif()

elseif(opt_lapack STREQUAL "mkl")
	message(FATAL_ERROR "not implemented!")
	elseif(opt_lapack STREQUAL "openblas")
	message(FATAL_ERROR "not implemented!")
else()
	message(STATUS "LAPACKE: off")
	message(STATUS "CBLAS: off")
endif()

# === Boost ===
if(opt_boost)
	message(STATUS "boost: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_BOOST\n")
	set(boost_lib boost_system boost_filesystem)
else()
	message(STATUS "boost: off")
endif()

# === GSL ===
if(opt_gsl)
	message(STATUS "gsl: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_GSL\n")
	set(gsl_lib gsl)
else()
	message(STATUS "gsl: off")
endif()

# === Eigen ===
if(opt_eigen)
	message(STATUS "eigen: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_EIGEN\n")
else()
	message(STATUS "eigen: off")
endif()

# === Quad Math ===
if(opt_quadmath)
	message(STATUS "quadmath: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_QUAD_MATH\n")
	add_compile_options(-fext-numeric-literals)
	set(quad_math_lib quadmath)
else()
	message(STATUS "quadmath: off")
endif()

# === Arb ===
if(opt_arb)
	message(STATUS "arb: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_ARB\n")
	set(arb_lib flint-arb flint mpfr gmp)
else()
	message(STATUS "arb: off")
endif()

# === MPLAPACK ===
if(opt_mplapack)
	message(STATUS "mplapack: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_MPLAPACK\n")
	set(mplapack_lib mplapack__Float128 mpblas__Float128)
else()
	message(STATUS "mplapack: off")
endif()

# === Arpack ===
if(opt_arpack)
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_ARPACK\n")
	if(NOT opt_long32)
		message(STATUS "arpack: 64")
		set(arpack_lib arpack64)
	else()
		message(STATUS "arpack: 32")
		set(arpack_lib arpack)
	endif()
else()
	message(STATUS "arpack: off")
endif()

# === SQLiteCpp ===
if(opt_sqlitecpp)
	set(opt_sqlite OFF)
	message(STATUS "sqlitecpp: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_SQLITECPP\n")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_SQLITE\n")
	set(sqlitecpp_lib SQLiteCpp sqlite3)
else()
	message(STATUS "sqlitecpp: off")
endif()

# === SQLite ===
if(opt_sqlite)
	message(STATUS "sqlite: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_SQLITE\n")
	set(sqlite_lib sqlite3)
else()
	message(STATUS "sqlite: off")
endif()

# === Matlab .mat file ===
# (conflicts with boost_filesystem.so other than version 1.56.0)
if(opt_matfile)
	message(STATUS "matfile: on")
	set(CONFIG_H "${CONFIG_H}#define SLS_USE_MATFILE\n")
	include_directories(../MatFile_linux/include)
	link_directories(../MatFile_linux/lib)
	set(matfile_lib mat mx)
	set(CMAKE_INSTALL_RPATH ../MatFile_linux/lib)
else()
	message(STATUS "matfile: off")
endif()



if(opt_compiler STREQUAL "g++")
	add_compile_options(-Wall -Wno-reorder -Wno-cpp -Wno-misleading-indentation -fopenmp -fmax-errors=2)
elseif(opt_compiler STREQUAL "clang++")
	add_compile_options(-Wall -Wno-reorder -Wno-overloaded-virtual -ferror-limit=5)
endif()

# =========== Done with Libs ===========

set(CONFIG_H "${CONFIG_H}#define SLS_FP_EXCEPT\n")
set(CONFIG_H "${CONFIG_H}#define SLS_USE_UTFCPP\n")

# write config.h
file(READ "SLISC/config.h" CONFIG_H_OLD)
if(NOT "${CONFIG_H}" STREQUAL "${CONFIG_H_OLD}")
	message(STATUS "config.h has changed.")
	file(WRITE "SLISC/config.h" ${CONFIG_H})
else()
	message(STATUS "config.h is the same.")
endif()

# get all .cpp and .h files
file(GLOB SOURCES "${PROJECT_SOURCE_DIR}/tests/*.cpp" "main.cpp")
file(GLOB HEADERS "${PROJECT_SOURCE_DIR}/SLISC/*.h" "${PROJECT_SOURCE_DIR}/SLISC/*/*.h")
file(GLOB HEADERS_IN "${PROJECT_SOURCE_DIR}/SLISC/*/*.h.in")
if (HEADERS_IN)
	string(REPLACE ".h.in" ".h"  HEADERS2 "${HEADERS_IN}")
	list(REMOVE_ITEM HEADERS ${HEADERS2})
endif()

# message(STATUS "HEADERS = ${HEADERS}")
# message(STATUS "HEADERS2 = ${HEADERS2}")

add_executable(main ${SOURCES} ${HEADERS} ${HEADERS2})
set_target_properties(main PROPERTIES OUTPUT_NAME main.x)

# scratch code
add_executable(scratch "scratch.cpp")
set_target_properties(scratch PROPERTIES OUTPUT_NAME scratch.x)

# === build *.h from *.h.in ===
if(opt_octave)
	file(GLOB H_INs "SLISC/*/*.h.in")
	# message(STATUS "H_Ins: ${H_INs}")
	set(octave_quadmath "false")
	if(opt_quadmath)
		set(octave_quadmath "true")
	endif()
	set(octave_long32 "false")
	if(opt_long32)
		set(octave_long32 "true")
	endif()
	foreach(H_IN ${H_INs})
		string(REPLACE ".h.in" ".h"  H_OUT "${H_IN}")
		get_filename_component(H_OUT0 "${H_OUT}" NAME)
		set(MY_CMD "octave --no-window-system --eval \"auto_gen({'../SLISC/','../tests/'}, '${H_OUT0}.in', ${octave_quadmath}, ${octave_long32})\"")
		string(REPLACE " " ";" MY_CMD ${MY_CMD})

		add_custom_command(OUTPUT "${H_OUT}"
		  COMMAND ${MY_CMD}
		  WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/preprocessor
		  DEPENDS "${H_IN}"
		  COMMENT "generating file: ${H_OUT}")
	endforeach()
endif()

# precompile header (total compilation time remains the same, but single recompilation might be slower. Can help to check `#pragma once` and `inline` before functions if they are missing)

# file(GLOB SLISC_HEADERS "SLISC/*.h")
# file(GLOB STL_HEADERS "/usr/include/c++/9/*")
# list(REMOVE_ITEM STL_HEADERS /usr/include/c++/9/backward /usr/include/c++/9/bits /usr/include/c++/9/debug /usr/include/c++/9/decimal /usr/include/c++/9/experimental /usr/include/c++/9/ext /usr/include/c++/9/parallel /usr/include/c++/9/profile /usr/include/c++/9/pstl /usr/include/c++/9/tr1 /usr/include/c++/9/tr2)
# target_precompile_headers(main.x PRIVATE ${STL_HEADERS} ${SLISC_HEADERS})
# target_include_directories(main PUBLIC "${CMAKE_CURRENT_BINARY_DIR}/SLISC")

# link
# (order is important)
target_link_libraries(main ${static_flag} ${arpack_lib} ${mplapack_lib} ${gsl_lib} ${mkl_lib} ${lapacke_lib} ${cblas_lib} ${arb_lib} ${boost_lib} ${matfile_lib} ${sqlite_lib} ${sqlitecpp_lib} ${quad_math_lib})

# clean_h
add_custom_target(clean_h
		  COMMAND rm -f ${HEADERS2} ${PROJECT_SOURCE_DIR}/tem_db.mat
		  COMMENT "removing generated headers and template database...")

# h
set(MY_CMD "octave --no-window-system --eval \"auto_gen({'../SLISC/','../tests/'}, [], ${octave_quadmath}, ${octave_long32})\"")
string(REPLACE " " ";" MY_CMD ${MY_CMD})
add_custom_target(h
		  COMMAND ${MY_CMD}
		  WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/preprocessor
		  DEPENDS ${HEADERS_IN}
		  COMMENT "generating all header files...")

message(STATUS "------------------------")
